evaluationDependsOn(':server')

apply plugin: 'com.android.application'


android {
    namespace 'org.client.scrcpy'
    flavorDimensions = ["default"]

    compileSdk = 36
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 35
        versionCode 9
        versionName "1.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
//        setProperty("archivesBaseName", "scrcpy")
    }
    signingConfigs {
        release {
            storeFile file("scrcpy.jks")

            def properties = new Properties()
            if (file("../local.properties").exists()) {
                file("../local.properties").withInputStream { stream ->
                    properties.load(stream)
                }
            }

            storePassword System.getenv("SIGNING_STORE_PASSWORD") ?: properties.getProperty("SIGNING_STORE_PASSWORD")
            keyAlias System.getenv("SIGNING_KEY_ALIAS") ?: properties.getProperty("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") ?: properties.getProperty("SIGNING_KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        scrcpy {
            applicationId "org.client.scrcpy"
        }
    }

    tasks.register('copyServer', Copy) {
        def buildType = gradle.startParameter.taskNames.any { it.endsWith('Release') } ? 'Release' : 'Debug'
        dependsOn 'deleteServer'
        dependsOn ":server:assemble${buildType}"

        def release_file = '../server/build/outputs/apk/release/server-release-unsigned.apk'
        def debug_file = '../server/build/outputs/apk/debug/server-debug.apk'
        def file_dest = '../app/src/main/assets/'

        if (buildType == "Debug") {
            from file(debug_file)
            into file(file_dest)
        } else {
            from file(release_file)
            into file(file_dest)
        }
        rename { fileName ->
            'scrcpy-server.jar'
        }
    }
    tasks.register('deleteServer', Delete) {
        delete "../app/src/main/assets/scrcpy-server.jar"
    }

    preBuild.dependsOn copyServer
}

dependencies {
    implementation project(":scrcpy-api")
    implementation project(":scrcpyx-core")
    implementation 'io.grpc:grpc-okhttp:1.77.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.77.0'
    implementation 'io.grpc:grpc-stub:1.77.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
