apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'
apply plugin: 'maven-publish'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.8"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.77.0'
        }
    }

    generateProtoTasks {
        all().forEach { task ->
            task.builtins {
                java { option 'lite' }
            }
            task.plugins {
                grpc { option 'lite' }
            }
        }
    }
}

android {
    namespace 'com.anonymous.scrcypx'

    compileSdk = 36
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 3
        versionName "1.0.6-SNAPSHOT"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        aidl true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = android.namespace
                artifactId = project.name
                version = "${android.defaultConfig.versionName}"

                from components.release
            }
        }
        repositories {
            mavenLocal()  // this will publish to local Maven repo (~/.m2/repository)
            maven {
                name = "private"
                url = android.defaultConfig.versionName.endsWith("-SNAPSHOT") ?
                        uri("http://repo.dev.sai/repository/maven-snapshots/") :
                        uri("http://repo.dev.sai/repository/maven-releases/")
                allowInsecureProtocol = true
                credentials {
                    username = "admin"
                    password = "admin"
                }
//                mavenContent {
//                    snapshotsOnly()
//                }
            }
        }
    }
}

dependencies {
    implementation project(":scrcpy-api")

    implementation 'io.grpc:grpc-okhttp:1.77.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.77.0'
    implementation 'io.grpc:grpc-stub:1.77.0'

    testImplementation 'junit:junit:4.13.2'
}

configurations.configureEach {
    resolutionStrategy.cacheDynamicVersionsFor 10, 'seconds'
}

